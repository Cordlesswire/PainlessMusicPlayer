apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"

    defaultConfig {
        applicationId "com.doctoror.fuckoffmusicplayer"
        minSdkVersion 21
        targetSdkVersion 25
        versionCode 1
        versionName "1.0.0"
        resConfigs "en"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    lintOptions {
        checkAllWarnings true
        disable 'GoogleAppIndexingWarning'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    signingConfigs {

        release {
            storeFile file("../keystore/doctoror.keystore")
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
        }

        release {
            signingConfig signingConfigs.release

            if (project.hasProperty('keyAlias')) {
                android.signingConfigs.release.keyAlias = keyAlias
            }

            if (project.hasProperty('storePassword')) {
                android.signingConfigs.release.storePassword = storePassword
            }

            if (project.hasProperty('keyPassword')) {
                android.signingConfigs.release.keyPassword = keyPassword
            }

            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            proguardFile 'proguard-eventbus.pro'
            proguardFile 'proguard-retrolambda.pro'
            proguardFile 'proguard-rx-java.pro'
        }
    }

    dataBinding {
        enabled = true
    }
}

dependencies {
    def supportVersion = '25.3.1'
    def gpsVersion = '10.2.1'

    testCompile 'junit:junit:4.12'
    // These dependencies are for promoting versions that used by
    // com.google.android.wearable:wearable and com.android.support.test:runner
    androidTestCompile "com.android.support:support-annotations:$supportVersion"
    androidTestCompile "com.android.support:support-v4:$supportVersion"
    androidTestCompile "com.android.support:percent:$supportVersion"
    androidTestCompile "com.android.support:recyclerview-v7:$supportVersion"

    androidTestCompile 'com.android.support.test:runner:0.5'
    androidTestCompile 'com.android.support.test:rules:0.5'

    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile project(':commons')

    provided 'com.google.android.wearable:wearable:2.0.1'
    compile 'com.google.android.support:wearable:2.0.1'

    // These dependencies are for promoting versions that used by
    // com.google.android.wearable:wearable and com.android.support.test:runner
    compile "com.android.support:recyclerview-v7:$supportVersion"
    compile "com.android.support:support-v4:$supportVersion"
    compile "com.android.support:percent:$supportVersion"

    compile "com.google.android.gms:play-services-wearable:$gpsVersion"
    compile 'com.android.support.constraint:constraint-layout:1.0.2'

    compile 'org.greenrobot:eventbus:3.0.0'
    compile 'io.reactivex.rxjava2:rxjava:2.0.8'
}
