apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'com.neenbedankt.android-apt'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"
    defaultConfig {
        applicationId "com.doctoror.fuckoffmusicplayer"
        minSdkVersion 16
        targetSdkVersion 25
        versionCode 1
        versionName "1.0.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        resConfigs "en"
    }

    lintOptions {
        checkAllWarnings true
        disable 'IconMissingDensityFolder'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    signingConfigs {

        release {
            storeFile file("../keystore/doctoror.keystore")
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
        }

        release {
            debuggable false
            signingConfig signingConfigs.release

            if (project.hasProperty('keyAlias')) {
                android.signingConfigs.release.keyAlias = keyAlias
            }

            if (project.hasProperty('storePassword')) {
                android.signingConfigs.release.storePassword = storePassword
            }

            if (project.hasProperty('keyPassword')) {
                android.signingConfigs.release.keyPassword = keyPassword
            }

            minifyEnabled true
            shrinkResources true

            proguardFile getDefaultProguardFile('proguard-android.txt')
            proguardFile 'proguard-dart.pro'
            proguardFile 'proguard-parceler.pro'
            proguardFile 'proguard-retrolambda.pro'
            proguardFile 'proguard-rules.pro'
            proguardFile 'proguard-rx-java.pro'
            proguardFile 'proguard-support-design.pro'
            proguardFile 'proguard-support-v7-appcompat.pro'
        }
    }

    splits {
        density {
            enable true
        }
    }

    dataBinding {
        enabled = true
    }

    apt {
        arguments {
            "dart.henson.package" "com.doctoror.fuckoffmusicplayer"
        }
    }
}

dependencies {
    def supportVersion = '25.1.0'
    def gpsVersion = '10.0.1'

    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })

    testCompile 'junit:junit:4.12'

    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile project(':commons')
    wearApp project(':wear')

    // Support library
    compile "com.android.support:support-v13:$supportVersion"
    compile "com.android.support:appcompat-v7:$supportVersion"
    compile "com.android.support:recyclerview-v7:$supportVersion"
    compile "com.android.support:design:$supportVersion"
    compile "com.android.support:cardview-v7:$supportVersion"
    compile 'com.android.support.constraint:constraint-layout:1.0.0-beta4'

    // Google Play Services
    compile "com.google.android.gms:play-services-wearable:$gpsVersion"

    // Views
    compile 'com.github.doctoror.aspectratiolayout:library:1.0.3'
    compile 'com.l4digital.fastscroll:fastscroll:1.0.1'
    compile 'com.android.support.constraint:constraint-layout:1.0.0-beta4'

    // RX
    compile 'io.reactivex:rxjava:1.2.4'
    compile 'io.reactivex:rxandroid:1.2.1'
    compile 'com.tbruyelle.rxpermissions:rxpermissions:0.9.1@aar'
    compile 'com.jakewharton.rxbinding:rxbinding-appcompat-v7:1.0.0'
    compile 'com.github.doctoror.rxcursorloader:library:1.0.4'

    // Injection
    compile 'com.jakewharton:butterknife:8.4.0'
    apt 'com.jakewharton:butterknife-compiler:8.4.0'
    compile 'com.f2prateek.dart:dart:2.0.1'
    provided 'com.f2prateek.dart:dart-processor:2.0.1'
    compile 'com.f2prateek.dart:henson:2.0.1'
    provided 'com.f2prateek.dart:henson-processor:2.0.1'

    // Generation
    compile 'org.parceler:parceler-api:1.1.6'
    apt 'org.parceler:parceler:1.1.6'
    compile 'com.google.protobuf.nano:protobuf-javanano:3.1.0'

    // Loader
    compile 'com.github.bumptech.glide:glide:3.7.0'

    // Media
    compile 'com.google.android.exoplayer:exoplayer:r2.1.1'
}
