apply plugin: 'com.android.application'

android {
    compileSdkVersion 27
    buildToolsVersion "27.0.3"

    defaultConfig {
        applicationId "com.doctoror.fuckoffmusicplayer"

        minSdkVersion 16
        targetSdkVersion 27

        versionCode 1
        versionName "1.0.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        resConfigs "en"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [ 'dart.henson.package' : 'com.doctoror.fuckoffmusicplayer' ]
            }
        }
    }

    lintOptions {
        checkAllWarnings true
        disable 'IconMissingDensityFolder'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    signingConfigs {

        release {
            storeFile file("../keystore/doctoror.keystore")
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
        }

        release {
            debuggable false
            signingConfig signingConfigs.release

            if (project.hasProperty('keyAlias')) {
                android.signingConfigs.release.keyAlias = keyAlias
            }

            if (project.hasProperty('storePassword')) {
                android.signingConfigs.release.storePassword = storePassword
            }

            if (project.hasProperty('keyPassword')) {
                android.signingConfigs.release.keyPassword = keyPassword
            }

            minifyEnabled true
            shrinkResources true

            proguardFile getDefaultProguardFile('proguard-android.txt')
            proguardFile 'proguard-dart.pro'
            proguardFile 'proguard-parceler.pro'
            proguardFile 'proguard-retrolambda.pro'
            proguardFile 'proguard-rules.pro'
            proguardFile 'proguard-rx-java.pro'
            proguardFile 'proguard-support-design.pro'
            proguardFile 'proguard-support-v7-appcompat.pro'
        }
    }

    splits {
        density {
            enable true
        }
    }

    dataBinding {
        enabled = true
    }
}

dependencies {
    def supportVersion = '27.1.0'

    testImplementation 'junit:junit:4.12'
    androidTestImplementation "com.android.support:support-annotations:$supportVersion"
    androidTestImplementation 'com.android.support.test:runner:1.0.1'
    androidTestImplementation 'com.android.support.test:rules:1.0.1'

    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation project(':siriwaveview')

    // Support library
    implementation "com.android.support:support-v13:$supportVersion"
    implementation "com.android.support:appcompat-v7:$supportVersion"
    implementation "com.android.support:recyclerview-v7:$supportVersion"
    implementation "com.android.support:design:$supportVersion"
    implementation "com.android.support:cardview-v7:$supportVersion"
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'

    // Views
    implementation 'com.github.doctoror.aspectratiolayout:library:1.0.3'
    implementation 'com.l4digital.fastscroll:fastscroll:1.0.4'

    // RX
    implementation 'io.reactivex.rxjava2:rxjava:2.1.10'
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.2'
    implementation 'com.tbruyelle.rxpermissions2:rxpermissions:0.9.5@aar'
    implementation 'com.jakewharton.rxbinding2:rxbinding-appcompat-v7:2.1.1'
    implementation 'com.github.doctoror.rxcursorloader:library:2.0.2'

    // Injection
    implementation 'com.google.dagger:dagger:2.14.1'
    annotationProcessor 'com.google.dagger:dagger-compiler:2.14.1'

    implementation 'com.jakewharton:butterknife:8.8.1'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.8.1'

    implementation 'com.f2prateek.dart:dart:2.0.3'
    annotationProcessor 'com.f2prateek.dart:dart-processor:2.0.3'

    implementation 'com.f2prateek.dart:henson:2.0.3'
    annotationProcessor 'com.f2prateek.dart:henson-processor:2.0.3'

    // Generation
    implementation 'org.parceler:parceler-api:1.1.10'
    annotationProcessor 'org.parceler:parceler:1.1.10'
    implementation 'com.google.protobuf.nano:protobuf-javanano:3.1.0'

    // Loader
    implementation 'com.github.bumptech.glide:glide:3.8.0'

    // Media
    implementation 'com.google.android.exoplayer:exoplayer:2.6.1'
}
