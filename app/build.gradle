apply plugin: 'com.android.application'

android {
    compileSdkVersion 27
    buildToolsVersion "27.0.3"

    defaultConfig {
        applicationId "com.doctoror.fuckoffmusicplayer"

        minSdkVersion 16
        targetSdkVersion 27

        versionCode 1
        versionName "1.0.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        resConfigs "en"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [ 'dart.henson.package' : 'com.doctoror.fuckoffmusicplayer' ]
            }
        }
    }

    lintOptions {
        checkAllWarnings true
        disable 'IconMissingDensityFolder'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    signingConfigs {

        release {
            storeFile file("../keystore/doctoror.keystore")
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
        }

        release {
            debuggable false
            signingConfig signingConfigs.release

            if (project.hasProperty('keyAlias')) {
                android.signingConfigs.release.keyAlias = keyAlias
            }

            if (project.hasProperty('storePassword')) {
                android.signingConfigs.release.storePassword = storePassword
            }

            if (project.hasProperty('keyPassword')) {
                android.signingConfigs.release.keyPassword = keyPassword
            }

            minifyEnabled true
            shrinkResources true

            proguardFile getDefaultProguardFile('proguard-android.txt')
            proguardFile 'proguard-dart.pro'
            proguardFile 'proguard-parceler.pro'
            proguardFile 'proguard-retrolambda.pro'
            proguardFile 'proguard-rules.pro'
            proguardFile 'proguard-rx-java.pro'
            proguardFile 'proguard-support-design.pro'
            proguardFile 'proguard-support-v7-appcompat.pro'
        }
    }

    flavorDimensions "default"

    productFlavors {
        default1 {

        }

        withWearable {

        }
    }

    splits {
        density {
            enable true
        }
    }

    dataBinding {
        enabled = true
    }
}

dependencies {
    def supportVersion = '25.3.1'
    def gpsVersion = '10.2.4'

    testCompile 'junit:junit:4.12'
    androidTestCompile "com.android.support:support-annotations:$supportVersion"
    androidTestCompile 'com.android.support.test:runner:0.5'
    androidTestCompile 'com.android.support.test:rules:0.5'

    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile project(':siriwaveview')
    compile project(':commons')
    withWearableWearApp project(':wear')

    // Support library
    compile "com.android.support:support-v13:$supportVersion"
    compile "com.android.support:appcompat-v7:$supportVersion"
    compile "com.android.support:recyclerview-v7:$supportVersion"
    compile "com.android.support:design:$supportVersion"
    compile "com.android.support:cardview-v7:$supportVersion"
    compile 'com.android.support.constraint:constraint-layout:1.0.2'

    // Google Play Services
    withWearableCompile "com.google.android.gms:play-services-wearable:$gpsVersion"

    // Views
    compile 'com.github.doctoror.aspectratiolayout:library:1.0.3'
    compile 'com.l4digital.fastscroll:fastscroll:1.0.2'

    // RX
    compile 'io.reactivex.rxjava2:rxjava:2.1.9'
    compile 'io.reactivex.rxjava2:rxandroid:2.0.1'
    compile 'com.tbruyelle.rxpermissions2:rxpermissions:0.9.4@aar'
    compile 'com.jakewharton.rxbinding2:rxbinding-appcompat-v7:2.0.0'
    compile 'com.github.doctoror.rxcursorloader:library:2.0.0'

    // Injection
    compile 'com.google.dagger:dagger:2.10'
    annotationProcessor 'com.google.dagger:dagger-compiler:2.10'

    compile 'com.jakewharton:butterknife:8.5.1'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.5.1'

    compile 'com.f2prateek.dart:dart:2.0.2'
    annotationProcessor 'com.f2prateek.dart:dart-processor:2.0.2'

    compile 'com.f2prateek.dart:henson:2.0.2'
    annotationProcessor 'com.f2prateek.dart:henson-processor:2.0.2'

    // Generation
    compile 'org.parceler:parceler-api:1.1.6'
    annotationProcessor 'org.parceler:parceler:1.1.6'
    compile 'com.google.protobuf.nano:protobuf-javanano:3.1.0'

    // Loader
    compile 'com.github.bumptech.glide:glide:3.8.0'

    // Media
    compile 'com.google.android.exoplayer:exoplayer:r2.3.1'
}
